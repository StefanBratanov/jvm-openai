plugins {
    id "java-library"
    id "jacoco"
    id "org.sonarqube" version "5.0.0.4638"
    id "maven-publish"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id "com.diffplug.spotless" version "6.25.0"
    id "me.qoomon.git-versioning" version "6.4.3"
    id "com.github.ben-manes.versions" version "0.51.0"
}

group = "io.github.stefanbratanov"

version = "develop"

gitVersioning.apply {
    refs {
        tag("v(?<version>.*)") {
            version = '${ref.version}'
        }
    }
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

def jacksonVersion = "2.17.1"
def junitVersion = "5.10.2"

dependencies {
    implementation("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}")
    api("com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}")

    testImplementation("org.junit.jupiter:junit-jupiter:${junitVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${junitVersion}")
    testImplementation("org.assertj:assertj-core:3.26.0")
    testImplementation("org.mock-server:mockserver-netty-no-dependencies:5.15.0")
    testImplementation("com.atlassian.oai:swagger-request-validator-core:2.40.0")
    testImplementation("org.skyscreamer:jsonassert:1.5.1")
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "jvm-openai"
                description = "A minimalistic OpenAI API client for the JVM, written in Java"
                url = "https://github.com/StefanBratanov/jvm-openai"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "StefanBratanov"
                        name = "Stefan Bratanov"
                        email = "stefan.bratanov93@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/StefanBratanov/jvm-openai.git"
                    developerConnection = "scm:git:ssh://github.com/StefanBratanov/jvm-openai.git"
                    url = "https://github.com/StefanBratanov/jvm-openai"
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(System.getenv("SIGNING_KEY"), System.getenv("SIGNING_PASSWORD"))
    sign publishing.publications
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_PASSWORD")
        }
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "FAILED"
        showStandardStreams = true
        exceptionFormat "full"
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonar {
    properties {
        property "sonar.projectKey", "StefanBratanov_jvm-openai"
        property "sonar.organization", "stefanbratanov"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}